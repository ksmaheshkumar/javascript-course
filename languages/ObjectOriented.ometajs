// Copyright (c) 2014 Quildreen Motta <quildreen@gmail.com>
//
// Permission is hereby granted, free of charge, to any person
// obtaining a copy of this software and associated documentation files
// (the "Software"), to deal in the Software without restriction,
// including without limitation the rights to use, copy, modify, merge,
// publish, distribute, sublicense, and/or sell copies of the Software,
// and to permit persons to whom the Software is furnished to do so,
// subject to the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
// EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
// MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
// NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
// LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
// OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
// WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
var parse              = require('esprima').parse
var extend             = require('xtend')
var path               = require('path')
var FunctionalCompiler = require('./Functional').Compiler
var FunctionalParser   = require('./Functional').Parser
var _                  = require('../lib/gen')
var runtime            = require('./runtime/core')

exports.name = 'Object Oriented'
exports.state = function() {
  return extend(runtime, {
    require: runtime.$require('.oo', path.join(__dirname, 'stdlib', 'oo'), '.')
  }) 
}

ometa Parser <: FunctionalParser {
  value = keyword('true')  -> [#Lit, true]
        | keyword('false') -> [#Lit, false]
        | number
        | string
        | tuple
        | record
        | vector
        | bottom,

  lambda = '(' ws listOf(#identifier, ','):as ws ')' ws "->" ws expr:b -> as.reduceRight(function(r, x) { return [#Lambda, x, r] }, b)
         | identifier:a ws "->" ws expr:b                              -> [#Lambda, a, b],
                               

  expr = expr:a ws keyword('where') ws listOf(#assign, ","):bs ws keyword('end') -> [#LetIn, bs, a]
       | fpipeExpr,

  fpipeExpr = fpipeExpr:a ws "|>" ws bpipeExpr:b -> [#App, a, b]
            | bpipeExpr,

  bpipeExpr = bpipeExpr:a ws "<|" ws ffiExpr:b -> [#App, b, a]
            | ffiExpr,

  ffiExpr = keyword('ffi') ws string:a -> [#FFI, a]
          | matchExpr
          | condExpr,

  primExpr = primExpr:a ws '#' ws identifier:b ws '(' ws listOf(#expr, ','):cs ws ')' -> (function(ref) {
               return [#App, [#Lambda, [#Id, ref], 
                               cs.reduce(function(r, x) { 
                                 return [#App, r, x] 
                               }, [#App, [#Member, [#Id, ref], b], [#Id, ref]])]
                             , a]
             }(FunctionalCompiler.freshName()))
           | primExpr:a ws '#' ws identifier:b  -> (function(ref) {
                                                      return [#App, [#Lambda, [#Id, ref], 
                                                                     [#App, [#Member, [#Id, ref], b], [#Id, ref]]], 
                                                              a]
                                                   }(FunctionalCompiler.freshName()))
           | primExpr:a ws '.' ws identifier:b ws '(' ws listOf(#expr, ','):cs ws ')' -> cs.reduce(function(r,x){ return [#App, r, x] }, [#Member, a, b])
           | primExpr:a ws '(' ws listOf(#expr, ','):bs ws ')' -> bs.reduce(function(x, b){ return [#App, x, b] }, a)
           | primExprHd,


  primExprHd = '(' ws expr:a ws ')' -> a
             | doExpr
             | lambda
             | qualId
             | value,

  doExpr  = keyword('do') (ws doChain)*:as ws doMap:b ws keyword('end') -> [#Do, as, b],
  doChain = identifier:a ws "<-" ws expr:b -> [#DChain, a, b],
  doMap   = keyword('return') ws expr:b    -> [#DMap, b],
  

  methodDecl = keyword('method') ws qualId:a ws '#' ws identifier:m ws '=' ws expr:e -> [#DefMeth, a, m, e]
             | keyword('method') ws qualId:a ws '#' ws identifier:m ws '(' ws listOf(#identifier, ','):cs ws ')' ws '=' ws expr:e -> [#DefMeth, a, m, cs.reduceRight(function(r, x) {
                                                                                                                                       return [#Lambda, x, r]
                                                                                                                                     }, e)],

  moduleDecl = keyword('module') (ws moduleStmt)*:as ws keyword('end')                                         -> [#SModule, as]
             | keyword('module') ws '(' listOf(#identifier, ','):bs ')' (ws moduleStmt)*:as ws keyword('end')  -> [#Module, bs, as],

  exportConstruct = keyword('export') ws identifier:a ws '=' ws expr:e           -> [#Export, a, e]
                  | keyword('export') ws identifier:a (ws ',' ws identifier)*:bs -> [#ExportAll, [a].concat(bs)]
                  | keyword('export') ws dataDecl:a                              -> [#ExportData, a[1], a],

  moduleStmt = exportConstruct
             | statement,

  statement = letConstruct
            | methodDecl
            | dataDecl
            | expr:a -> [#StmtEx, a],

  
  program = ws moduleDecl:a ws eof    -> [#Program, [a]]
          | (ws statement)*:xs ws eof -> [#Program, xs],
}

ometa Compiler <: FunctionalCompiler {
  DefMeth cc:a cc:m cc:e   -> _.expr(_.call(_.id('$defMethod'), [a, _.lit(m.name), e])),
  Module [cc*:as] [cc*:bs] -> _.expr(_.exportMod(_.module(as, bs))),
  SModule [cc*:bs]         -> _.expr(_.exportMod(_.call(_.module([], bs), []))),
  Export cc:a cc:b         -> _.var(a, _.visible(a, b)),
  ExportAll [cc*:as]       -> _.block(as.map(function(a) {
                                return _.expr(_.visible(a, a))
                              })),
  ExportData cc:a cc:b     -> _.block([b, _.expr(_.visible(a, a))]),
  FFI cc:a                 -> parse(a.value).body[0].expression,
  Vector [cc*:as]          -> as.reduceRight(function(r, a) {
                                return _.call(_.member(_.id('$List'), _.id('Cons')), [a, r])
                              }, _.member(_.id('$List'), _.id('Nil'))),
  Do [cc*:as] cc:b         -> as.reduceRight(function(r, a, i) {
                                var m = i === as.length - 1? 'map' : 'chain'
                                var n = _.id(FunctionalCompiler.freshName())
                                var l = _.fn(null, [n], _.block([
                                          _.return(_.call(
                                            _.methodCall(n, _.id(m), [n]),
                                            [_.lambda(a[0], r)]
                                          ))
                                        ]), {})
                                return _.call(l, [a[1]])
                              }, b),
  DMap cc:b                -> b,
  DChain cc:a cc:b         -> [a, b]
}


